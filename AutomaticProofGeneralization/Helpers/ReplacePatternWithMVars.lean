import Lean
import AutomaticProofGeneralization.Helpers.Antiunification
import AutomaticProofGeneralization.Helpers.Naming
import AutomaticProofGeneralization.Helpers.Metavariables
import AutomaticProofGeneralization.Helpers.FunctionApplications

open Lean Elab Tactic Meta Term Command AntiUnify

initialize
  registerTraceClass `TypecheckingErrors
  registerTraceClass `Autogeneralize.abstractPattern

local instance {Œ±} : ExceptToEmoji Exception Œ± where
  toEmoji
  | .error _ => crossEmoji
  | .ok _ => ""

section ReplacePatternWithMVars

/-!

This section of the file defines `replacePatternWithMVars`,
a function that traverses an expression replacing
all instances of a pattern with (different) metavariables and
abstracting relevant hypotheses.

It is loosely based on the `Lean.Meta.kabstract` from Lean's core library.

The function runs in the `ReplaceM` monad defined below,
which keeps track of relevant data during the traversal.

-/

/-- The read-only part of the `ReplaceM` monad. -/
structure ReplaceM.Context where
  /-- The local context within which new metavariables are defined. -/
  lctx : LocalContext := {}
  /-- The local instances within which new metavariables are defined. -/
  localInstances : LocalInstances := {}
  /-- Whether to abstract hypotheses in addition to replacing occurrences of the pattern with metavariables. -/
  abstractHyps : Bool := true
  /-- Whether to identify constants in the proof that depend on the one being generalized. -/
  generalizeRelatedConstants : Bool := true
  /-- The current recursion depth of the function. -/
  depth : Nat := 0
  /-- The maximum cutoff recursion depth of the function. -/
  cutOffDepth : Nat := 2

/-- The stateful part of the `ReplaceM` monad. -/
structure ReplaceM.State where
  /-- A list of terms identified to be conflicting with the one being generalized. -/
  mismatches : List Expr := []
  /-- Whether the expression has been modified during the traversal. -/
  modified : Bool := false

/-- The `ReplaceM` monad, which keeps track of relevant data needed to abstract instances of a pattern within an expression. -/
abbrev ReplaceM := ReaderT ReplaceM.Context <| StateT ReplaceM.State MetaM

/-- Adds a list of terms to the list of conflicting terms in the state. -/
def ReplaceM.addConflictingTerms (terms : List Expr) : ReplaceM Unit := do
  modify fun œÉ => { œÉ with mismatches := (terms ++ œÉ.mismatches).eraseDups }

/-- Return the result if the action changes the `modified` flag to `true`, return `none` otherwise. -/
def ReplaceM.withReturnIfModified {Œ±} (act : ReplaceM Œ±) : ReplaceM (Option Œ±) := do
  modify ({¬∑ with modified := false})
  let result ‚Üê act
  if (‚Üê get).modified then
    return some result
  else
    return none

/-- Create a new metavariable of the given type, marking the expression as `modified` in the process. -/
def ReplaceM.mkExprMVar (type : Expr) (userName : Name := .anonymous) : ReplaceM Expr := do
  let ctx ‚Üê read
  let mvar ‚Üê mkFreshExprMVarAt ctx.lctx ctx.localInstances type (kind := .synthetic) (userName := userName)
  modify ({ ¬∑ with modified := true })
  return mvar

def ReplaceM.withIncDepth {Œ±} : ReplaceM Œ± ‚Üí ReplaceM Œ± :=
  withReader fun ctx ‚Ü¶ { ctx with depth := ctx.depth + 1 }

def ReplaceM.withoutAbstractingHypsOrGeneralizingConstants {Œ±} : ReplaceM Œ± ‚Üí ReplaceM Œ± :=
  withReader ({ ¬∑ with abstractHyps := false, generalizeRelatedConstants := false })

def ReplaceM.run {Œ±} (m : ReplaceM Œ±) : MetaM (Œ± √ó ReplaceM.State) := do
  ReaderT.run m { lctx := ‚Üê getLCtx, localInstances := ‚Üê getLocalInstances } |>.run {}

def ReplaceM.run' {Œ±} (m : ReplaceM Œ±) : MetaM Œ± := do
  ReaderT.run m { lctx := ‚Üê getLCtx, localInstances := ‚Üê getLocalInstances } |>.run' {}

open ReplaceM in
/--
`replacePatternWithMVars` takes in an expression `e` and a pattern `p` as input
and abstracts all occurrences of `p` in `e` by replacing them with *different* metavariables,
abstracting relevant hypotheses and identifying related constants to generalize in the process.
-/
partial def replacePatternWithMVars (e : Expr) (p : Expr) : ReplaceM Expr := do
  -- the type of the pattern being generalized
  let pType ‚Üê withReducibleAndInstances <| reduce <| ‚Üê inferType p
  trace[Autogeneralize.abstractPattern] m!"Abstracting pattern {p} of type {pType}"
  let rec visit (e : Expr) : ReplaceM Expr := do
    let visitChildren : Unit ‚Üí ReplaceM Expr := fun _ => do
      match e with
      | .app f a         =>
        -- it's possible that modifications to the expression during the traversal create application type mismatches
        -- so it may be necessary to identify additional terms to generalize to make the overall expression type-correct.
        let fAbs ‚Üê visit f
        let aAbs ‚Üê visit a
        if (‚Üê read).generalizeRelatedConstants then
          let expectedA ‚Üê extractArgType fAbs -- the expected type of the argument
          let inferredA ‚Üê inferType aAbs      -- the actual type of the argument
          -- if there is an application type mismatch, identify new terms to generalize
          unless ‚Üê liftM <| withoutModifyingState <| isDefEq expectedA inferredA do
            trace[TypecheckingErrors] m!"Error in typechecking: aAbs was expected to have type \n\t{expectedA} \nbut has type \n\t{inferredA}"
            -- the mismatch is probably caused because something else needs to be generalized
            -- candidate terms for generalization are identified through anti-unification
            let (_result, problemTerms) ‚Üê getTermsToGeneralize expectedA inferredA
            trace[TypecheckingErrors] m!"The mismatch can probably be fixed by generalizing the terms {problemTerms}"
            -- add the conflicting terms to the state
            ReplaceM.addConflictingTerms (problemTerms.map Prod.snd)
        return e.updateApp! fAbs aAbs
      | .mdata _ b       => return e.updateMData! (‚Üê visit b)
      | .proj _ _ b      => return e.updateProj! (‚Üê visit b)
      | .letE n t v b _ =>
        let tAbs ‚Üê visit t
        let vAbs ‚Üê visit v
        -- ensure that the generalized value and type are still in sync
        unless ‚Üê isDefEq tAbs (‚Üê inferType vAbs) do
          throwError m!"Expected the type of {vAbs} in `let` statement to be {tAbs}, but got {‚Üê inferType vAbs}"
        -- "withLetDecl" temporarily adds "n : tAbs" to context, storing the fvar in placeholder
        let updatedLet ‚Üê withLetDecl n tAbs vAbs fun placeholder => do
          let b := b.instantiate1 placeholder
          let bAbs ‚Üê if (‚Üê  liftM <| withoutModifyingState <| isDefEq tAbs t) then
                visit b  -- now it's safe to recurse on b (no loose bvars)
              else
                logInfo m!"tAbs {tAbs} and t {t} are not defeq"
                pure b
          return ‚Üê mkLetFVars (usedLetOnly := false) #[placeholder] bAbs-- put the "n:tAbs" back in the expression itself instead of in an external fvar

        return updatedLet
      | .lam n d b bi     =>
        let dAbs ‚Üê visit d
        -- "withLocalDecl" temporarily adds "n : dAbs" to context, storing the fvar in placeholder
        let updatedLambda ‚Üê withLocalDecl n bi dAbs fun placeholder => do
          let b := b.instantiate1 placeholder
          let bAbs ‚Üê
            if (‚Üê  liftM <| withoutModifyingState <| isDefEq dAbs d) then
              visit b  -- now it's safe to recurse on b (no loose bvars)
            else
              logInfo m!"dAbs {dAbs} and d {d} are not defeq"
              pure b
          return ‚Üê mkLambdaFVars #[placeholder] bAbs (binderInfoForMVars := bi) -- put the "n:dAbs" back in the expression itself instead of in an external fvar

        return updatedLambda
      | .forallE n d b bi =>
        let dAbs ‚Üê visit d
        -- "withLocalDecl" temporarily adds "n : dAbs" to context, storing the fvar in placeholder
        let updatedForAll ‚Üê withLocalDecl n bi dAbs fun placeholder => do
          let b := b.instantiate1 placeholder
          let bAbs ‚Üê visit b  -- now it's safe to recurse on b (no loose bvars)
          return ‚Üê mkForallFVars #[placeholder] bAbs (binderInfoForMVars := bi) -- put the "n:dAbs" back in the expression itself instead of in an external fvar
        return updatedForAll
      | e                =>
        return e

    withTraceNodeBefore `Autogeneralize.abstractPattern (pure m!"Visiting {e} at depth {(‚Üê read).depth}") do
      let mctx ‚Üê getMCtx
      -- if the expression "e" matches with the pattern being replaced ...
      if !e.isMVar && (‚Üê withReducibleAndInstances <| isDefEq e p) then
        -- since the type of `p` may be slightly different each time depending on the context it's in, we specialize it each time
        let pType ‚Üê instantiateMVars pType
        setMCtx mctx
        trace[Autogeneralize.abstractPattern] m!"{checkEmoji} Found pattern {p}, replacing with mvar"
        mkExprMVar pType (userName := placeholderName) -- replace every occurrence of pattern with mvar
      -- otherwise, "e" might contain the pattern...
      else
        setMCtx mctx
        -- so that other matches are still possible.
        let e' ‚Üê visitChildren ()

        if (‚Üê read).depth > (‚Üê read).cutOffDepth || !(‚Üê read).abstractHyps then
          return e'
        else
          -- if the current expression contains the pattern in its type (but not its term),
          -- we abstract it as a hypothesis of the generalized type

          -- the type of the expression being generalized
          let e'Type ‚Üê inferType e'
          trace[Autogeneralize.abstractPattern] m!"üîç Inspecting type {e'Type}"
          -- visiting the type of the expression, replacing any occurrences of the pattern with metavariables
          -- `withReturnIfModified` will return `none` if the type is not modified
          let some e'TypeAbs ‚Üê
            withReturnIfModified <| withIncDepth <| withoutAbstractingHypsOrGeneralizingConstants <|
              visit e'Type | return e'
          trace[Autogeneralize.abstractPattern] m!"‚≠ê Generating hypothesis {e'TypeAbs}"
          mkExprMVar e'TypeAbs -- mvar for generalized hypothesis
            -- (userName := mkAbstractedName n)
  visit e

end ReplacePatternWithMVars

/- Just like kabstract, except abstracts to mvars instead of bvars
  We use this to abstract the theorem TYPE...so we can re-specialize non-abstracted occurrences in the proof.
  -/
def abstractToOneMVar (thmType : Expr) (pattern : Expr) (occs : Occurrences) : MetaM Expr := do
  let userThmType ‚Üê kabstract thmType pattern (occs)

  let userMVar ‚Üê  mkFreshExprMVar (‚Üê inferType pattern)
  let annotatedMVar := Expr.mdata {entries := [(`userSelected,.ofBool true)]} $ userMVar
  let userThmType := userThmType.instantiate1 annotatedMVar

  return userThmType

/-
  Just like kabstract, except abstracts to different variables instead of the same one
  We use this to abstract the theorem TYPE...so we can re-specialize non-abstracted occurrences in the proof.
-/
def abstractToDiffMVars (e : Expr) (p : Expr) (occs : Occurrences) : MetaM Expr := do
  let pType ‚Üê inferType p
  let pHeadIdx := p.toHeadIndex
  let pNumArgs := p.headNumArgs
  let rec visit (e : Expr) : StateRefT Nat MetaM Expr := do
    let visitChildren : Unit ‚Üí StateRefT Nat MetaM Expr := fun _ => do
      match e with
      | .app f a         => return e.updateApp! (‚Üê visit f ) (‚Üê visit a )
      | .mdata _ b       => return e.updateMData! (‚Üê visit b )
      | .proj _ _ b      => return e.updateProj! (‚Üê visit b )
      | .letE _ t v b _  => return e.updateLet! (‚Üê visit t ) (‚Üê visit v ) (‚Üê visit b )
      | .lam _ d b _     => return e.updateLambdaE! (‚Üê visit d ) (‚Üê visit b )
      | .forallE _ d b _ => return e.updateForallE! (‚Üê visit d ) (‚Üê visit b )
      | e                => return e
    if e.hasLooseBVars then
      visitChildren ()
    else if e.toHeadIndex != pHeadIdx || e.headNumArgs != pNumArgs then
      visitChildren ()
    else
      -- We save the metavariable context here,
      -- so that it can be rolled back unless `occs.contains i`.
      let mctx ‚Üê getMCtx
      if (‚Üê isDefEq e p) then
        let i ‚Üê get
        set (i+1)
        if occs.contains i then
          let userMVar ‚Üê mkFreshExprMVar pType
          let annotatedMVar := Expr.mdata {entries := [(`userSelected,.ofBool true)]} $ userMVar
          return annotatedMVar
        else
          -- Revert the metavariable context,
          -- so that other matches are still possible.
          setMCtx mctx
          visitChildren ()
      else
        visitChildren ()
  visit e |>.run' 1
